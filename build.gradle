plugins {
    id "io.spring.dependency-management" version "1.1.7"
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.github.node-gradle.node" version "7.1.0"
}


node {
    version = '20.11.0'
    npmVersion = '10.2.4'
    download = true
    workDir = layout.buildDirectory.dir('ui').get().asFile
    npmWorkDir = file("${project.rootDir}/ui")
    nodeProjectDir = file("${project.rootDir}/ui")

}

npm_run_build {
    inputs.files fileTree("ui/src")
    inputs.file 'ui/package.json'
    inputs.file 'ui/package-lock.json'
    outputs.dir 'ui/build'
}

apply from: 'project-properties.gradle'

repositories {
    mavenCentral()

    if (!releaseMode) {
        maven {
            url = 'https://jitpack.io'
        }
    }
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:3.4.4")
        mavenBom("com.epam.reportportal:commons-bom:5.13.2")
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.epam.reportportal:plugin-api:5.12.0'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.12.0'
    }

    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.required.set(true)
    }
}

build.dependsOn jacocoTestReport

artifacts {
    archives shadowJar
}

sourceSets {
    main {
        resources
                {
                    exclude '**'
                }
    }
}

jar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    manifest {
        attributes(
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                "Plugin-Class": "${pluginClass}",
                "Plugin-Id": "${pluginId}",
                "Plugin-Name": "${pluginName}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "${provider}",
                "Plugin-License": "${license}",
                "Plugin-Description": "${project.description}",
                "Plugin-Documentation": "${documentation}",
                "Plugin-Metadata-isIntegrationsAllowed": "${isIntegrationsAllowed}",
                "Plugin-Metadata-embedded": "${isEmbedded}",
                "Plugin-Metadata-isMultiple": "${isMultiple}",
                "Plugin-Binary-Data-main": "${mainBinaryData}",
                "Plugin-Metadata-yourAdditionalMetadata": "${yourAdditionalMetadata}",
                "Plugin-Property-yourAdditionalProperty": "${yourAdditionalProperty}",
        )
    }
}

shadowJar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    configurations = [project.configurations.runtimeClasspath]
    zip64 = true
    dependencies {
    }
}

tasks.register('plugin', Jar) { task ->
    getArchiveBaseName().set("plugin-${pluginId}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.runtimeClasspath
    }
    getArchiveExtension().set('zip')
}

tasks.register('assemblePlugin', Copy) {
    from plugin
    into pluginsDir
}

tasks.register('assemblePlugins', Copy) {
    dependsOn subprojects*.tasks*.assemblePlugin
}

compileJava.dependsOn npm_run_build

